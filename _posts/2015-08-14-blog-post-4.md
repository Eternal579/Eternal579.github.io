---
title: 'How to run p4 program on Tofino1 happily'
date: 2024-09-30
permalink: /posts/2024/09/How-to-run-p4-program-on-Tofino1-happily/
tags:
  - p4
---

This is a blog to teach myself(future) to run p4 program happily. In fact, after submitting SOSP'24 poster, i have spent almost a week to run p4 program on Tofino1, the decline of the p4 community and the lack of docs is an absolute nightmare!

# write a simple p4 program?
## setup
- echo `$SDE` at your Tofino1, usually, it locates at `/opt/bf-sde-*-*-*`, if not, `export` it.
```
export SDE=/opt/bf-sde-*-*-*
export SDE_INSTALL=/opt/bf-sde-*.*.*/install
export PATH=/$SDE_INSTALL/bin:$PATH
```
- load bf driver, and check
```
$SDE_INSTALL/bin/bf_kdrv_mod_load $SDE_INSTALL
ls /dev/bf0
```
- for convenience, set our p4 program src root, you can find lots of examples under `$P4_SRC`
```
export P4_SRC=$SDE/pkgsrc/p4-examples/p4_16_programs/
```

## start a p4 program
- learn from [p4-learning](https://github.com/nsg-ethz/p4-learning). **NOTE**: it uses `v1model` architecture, tofino1 is `tna` architecture, but the difference is negligible, mainly in the final `main` section and the change to include `tna.h`.
- p4-16 has a more comprehensive documentation avaliable for reference, you can find every structure [here](http://eternal579.github.io/files/PUBLIC_Tofino_Native_Arch.pdf).
- here is an example to illustrate how to write a p4 program. **NOTE**: this example is used to realize ipv4-based-forwarding, since i do not how to bind `mcast_grp_a` to multiple ports to realize l2-flooding(No doc!!!), i forward arp packet based its `dstIpv4Addr`.

```
/* P4_16 */

#include <core.p4>
#include <tna.p4>

/*************************************************************************
 ************* C O N S T A N T S    A N D   T Y P E S  *******************
**************************************************************************/
const bit<16> ETHERTYPE_IPV4 = 0x0800;
const bit<16> ETHERTYPE_ARP = 0x0806;

// rixin: the num of host interfaces.
const int HOST_IF_NUM = 32;

typedef bit<48> macAddr_t;

/*************************************************************************
 ***********************  H E A D E R S  *********************************
 *************************************************************************/

/*  Define all the headers the program will recognize             */
/*  The actual sets of headers processed by each gress can differ */

/* Standard ethernet header */

header ethernet_h {
    bit<48>   dstAddr;
    bit<48>   srcAddr;
    bit<16>   ether_type;
}

header ipv4_h {
    bit<4>   version;
    bit<4>   ihl;
    bit<8>   diffserv;
    bit<16>  total_len;
    bit<16>  identification;
    bit<3>   flags;
    bit<13>  frag_offset;
    bit<8>   ttl;
    bit<8>   protocol;
    bit<16>  hdr_checksum;
    bit<32>  srcAddr;
    bit<32>  dstAddr;
}

header arp_h {
    bit<16> hw_type;
    bit<16> proto_type;
    bit<8> hw_sz;
    bit<8> proto_sz;
    bit<16> opcode;
    macAddr_t srcMacAddr;
    bit<32> srcIpv4Addr;
    macAddr_t dstMacAddr;
    bit<32> dstIpv4Addr;
}

struct my_ingress_headers_t {
    ethernet_h   ethernet;
    ipv4_h       ipv4;
    arp_h arp;
}

/******  G L O B A L   M E T A D A T A  *********/

struct my_ingress_metadata_t {
}

/*************************************************************************
 **************  I N G R E S S   P R O C E S S I N G   *******************
 *************************************************************************/

    /***********************  P A R S E R  **************************/
parser IngressParser(packet_in        pkt,
    /* User */    
    out my_ingress_headers_t          hdr,
    out my_ingress_metadata_t         meta,
    /* Intrinsic */
    out ingress_intrinsic_metadata_t  ig_intr_md)
{
    /* This is a mandatory state, required by Tofino Architecture */
     state start {
        pkt.extract(ig_intr_md);
        pkt.advance(PORT_METADATA_SIZE);
        transition parse_ethernet;
    }

    state parse_ethernet {
        pkt.extract(hdr.ethernet);
        transition select(hdr.ethernet.ether_type) {
            ETHERTYPE_IPV4:  parse_ipv4;
	    ETHERTYPE_ARP:   parse_arp;
            default: accept;
        }
    }

    state parse_ipv4 {
        pkt.extract(hdr.ipv4);
        transition accept;
    }

    state parse_arp {
	pkt.extract(hdr.arp);
	transition accept;
    }
}

    /***********************  M A U  **************************/
control Ingress(
    /* User */
    inout my_ingress_headers_t                       hdr,
    inout my_ingress_metadata_t                      meta,
    /* Intrinsic */
    in    ingress_intrinsic_metadata_t               ig_intr_md,
    in    ingress_intrinsic_metadata_from_parser_t   ig_prsr_md,
    inout ingress_intrinsic_metadata_for_deparser_t  ig_dprsr_md,
    inout ingress_intrinsic_metadata_for_tm_t        ig_tm_md) {

	action forward(PortId_t egress_port) {
	    ig_tm_md.ucast_egress_port = egress_port;
	}

	table l3_table {
	    key = {
	    	hdr.ipv4.dstAddr : exact;
	    }
	    actions = {
		forward;
		@defaultonly NoAction;
	    }
	    size = HOST_IF_NUM;
	}

	table arp_table {
	    key = {
	   	hdr.arp.dstIpv4Addr : exact;
	    }
	    actions = {
	   	forward;
		@defaultonly NoAction;
	    }
	    size = HOST_IF_NUM;
	}

	apply {
	    if (hdr.ethernet.ether_type == ETHERTYPE_IPV4) {
		l3_table.apply();
	    }
	    else {
	   	arp_table.apply();
	    }
	}
}

    /*********************  D E P A R S E R  ************************/
control IngressDeparser(packet_out pkt,
    /* User */
    inout my_ingress_headers_t                       hdr,
    in    my_ingress_metadata_t                      meta,
    /* Intrinsic */
    in    ingress_intrinsic_metadata_for_deparser_t  ig_dprsr_md)
{
    apply {
        pkt.emit(hdr);
    }
}

/*************************************************************************
 ****************  E G R E S S   P R O C E S S I N G   *******************
 *************************************************************************/

    /***********************  H E A D E R S  ************************/

struct my_egress_headers_t {
}

    /********  G L O B A L   E G R E S S   M E T A D A T A  *********/

struct my_egress_metadata_t {
}

    /***********************  P A R S E R  **************************/

parser EgressParser(packet_in        pkt,
    /* User */
    out my_egress_headers_t          hdr,
    out my_egress_metadata_t         meta,
    /* Intrinsic */
    out egress_intrinsic_metadata_t  eg_intr_md)
{
    /* This is a mandatory state, required by Tofino Architecture */
    state start {
        pkt.extract(eg_intr_md);
        transition accept;
    }
}

    /***************** M A T C H - A C T I O N  *********************/

control Egress(
    /* User */
    inout my_egress_headers_t                          hdr,
    inout my_egress_metadata_t                         meta,
    /* Intrinsic */    
    in    egress_intrinsic_metadata_t                  eg_intr_md,
    in    egress_intrinsic_metadata_from_parser_t      eg_prsr_md,
    inout egress_intrinsic_metadata_for_deparser_t     eg_dprsr_md,
    inout egress_intrinsic_metadata_for_output_port_t  eg_oport_md)
{
    apply {
    }
}

    /*********************  D E P A R S E R  ************************/

control EgressDeparser(packet_out pkt,
    /* User */
    inout my_egress_headers_t                       hdr,
    in    my_egress_metadata_t                      meta,
    /* Intrinsic */
    in    egress_intrinsic_metadata_for_deparser_t  eg_dprsr_md)
{
    apply {
        pkt.emit(hdr);
    }
}


/************ F I N A L   P A C K A G E ******************************/
Pipeline(
    IngressParser(),
    Ingress(),
    IngressDeparser(),
    EgressParser(),
    Egress(),
    EgressDeparser()
) pipe;

Switch(pipe) main;
```

## write the controller script
- we define two tables above, now, how to add entries in it? (this problem has been driving me crazy for two days)
- use python script, API is hidden [here](http://eternal579.github.io/files/p4_control_API.zip)
- download above `.zip` file, copy `tests.py` below, use VS Code to fix the `import` problem

```
################################################################################
# BREFOOT NETWORKS CONFIDENTIAL & PROPRIETARY
#
# Copyright (c) 2019-present Barefoot Networks, Inc.
#
# All Rights Reserved.
#
# NOTICE: All information contained herein is, and remains the property of
# Barefoot Networks, Inc. and its suppliers, if any. The intellectual and
# technical concepts contained herein are proprietary to Barefoot Networks, Inc.
# and its suppliers and may be covered by U.S. and Foreign Patents, patents in
# process, and are protected by trade secret or copyright law.  Dissemination of
# this information or reproduction of this material is strictly forbidden unless
# prior written permission is obtained from Barefoot Networks, Inc.
#
# No warranty, explicit or implicit is provided, unless granted under a written
# agreement with Barefoot Networks, Inc.
#
################################################################################

######### STANDARD MODULE IMPORTS ########
import logging 
import grpc   
import pdb

######### PTF modules for BFRuntime Client Library APIs #######
import ptf
from ptf.testutils import *
from bfruntime_client_base_tests import BfRuntimeTest
import bfrt_grpc.bfruntime_pb2 as bfruntime_pb2
import bfrt_grpc.client as gc

class rixinTest(BfRuntimeTest):
    def __init__(self):
        super(rixinTest, self).__init__()

    def setUp(self):
        print("\n==============RixinTest Setup=============\n")
        self.client_id = 0
        self.dev = 0
        self.p4_name = "rixin_test"
        self.target = gc.Target(self.dev, pipe_id=0xffff)
        BfRuntimeTest.setUp(self, self.client_id, self.p4_name)
        self.bfrt_info = self.interface.bfrt_info_get(self.p4_name)
        self.l3_table = self.bfrt_info.table_get("Ingress.l3_table")
        self.arp_table = self.bfrt_info.table_get("Ingress.arp_table")

        ip_port_mapping = [
            ("192.168.201.1", 141), ("192.168.202.1", 133),
            ("192.168.201.2", 135), ("192.168.202.2", 140),
            ("192.168.201.3", 142), ("192.168.202.3", 134),
            ("192.168.201.4", 132), ("192.168.202.4", 132),
            ("192.168.201.5", 148), 
            ("192.168.201.6", 156)]
    
        self.l3_table.info.key_field_annotation_add(
                            "hdr.ipv4.dstAddr", "ipv4")
        for (ip, port) in ip_port_mapping :
            self.l3_table.entry_add(
                self.target,
                [
                    self.l3_table.make_key(
                        [
                            gc.KeyTuple('hdr.ipv4.dstAddr', ip)
                        ],
                    )
                ],
                [
                    self.l3_table.make_data(
                        [
                            gc.DataTuple('egress_port', port)
                        ],
                        "Ingress.forward"
                    )
                ]
            )
        print("\n========================Added entries to l3_table==============================\n")

        self.arp_table.info.key_field_annotation_add(
                            "hdr.arp.dstIpv4Addr", "ipv4")
        for (ip, port) in ip_port_mapping :
            self.arp_table.entry_add(
                self.target,
                [
                    self.arp_table.make_key(
                        [
                            gc.KeyTuple('hdr.arp.dstIpv4Addr', ip)
                        ],
                    )
                ],
                [
                    self.arp_table.make_data(
                        [
                            gc.DataTuple('egress_port', port)
                        ],
                        "Ingress.forward"
                    )
                ]
            )
        print("\n========================Added entries to arp_table==============================\n")

    def runTest(self):
        print("\n==========================This is a empty runTest=======================\n")


```
- the above `tests.py` add entries into `l3_table` and `arp_table`, **NOTE**: remeber define the `runTest` method!!!

## run it!!!
- compile your p4 program. **NOTE**: re-compiling the p4 program can clean up the tables in Tofino1, but just re-deploying it won't.
```
cd $SDE/pkgsrc/p4-build
./autogen.sh
$SDE/pkgsrc/p4-build/configure --with-tofino --with-p4c=p4c --prefix=$SDE_INSTALL --bindir=$SDE_INSTALL/bin P4_NAME=rixin_test P4_PATH=$SDE/pkgsrc/p4-examples/p4_16_programs/rixin_test/rixin_test.p4 P4_VERSION=p4-16 P4_ARCHITECTURE=tna LDFLAGS="-L$SDE_INSTALL/lib"
make
make instal
```
- deploy your p4 program on the switch
```
$SDE/run_switchd.sh -p rixin_test
```
- run the controller script
```
$SDE/run_p4_test.sh -p rixin_test
```
- **NOTE**: the above `run_switchd.sh` and `run_p4_test.sh` originally come with Tofino1.